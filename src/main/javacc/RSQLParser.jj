/*
 * The MIT License
 *
 * Copyright 2013-2016 Jakub Jirutka <jakub@jirutka.cz>.
 * Copyright 2024 Edgar Asatryan <nstdio@gmail.com>.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

options {
    LOOKAHEAD = 1;
    CHOICE_AMBIGUITY_CHECK = 3;
    OTHER_AMBIGUITY_CHECK = 2;
    STATIC = false;
    DEBUG_PARSER = false;
    DEBUG_LOOKAHEAD = false;
    DEBUG_TOKEN_MANAGER = false;
    UNICODE_INPUT = true;
    SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
}

PARSER_BEGIN(Parser)

package cz.jirutka.rsql.parser;

import cz.jirutka.rsql.parser.ast.*;
import java.io.InputStream;
import java.io.Reader;
import java.util.ArrayList;
import java.util.Arrays;
import java.util.Collections;
import java.util.List;

final class Parser {

    private NodesFactory factory;

    public Parser(Reader reader, NodesFactory factory) {
        this(reader);
        this.factory = factory;
    }

    static String unescape(String s) {
        int start = 1;
        StringBuilder sb = null;

        for (int end; ; ) {
            end = s.indexOf('\\', start != 1 ? start + 1 : start);

            if (end < 0) {
                return sb == null
                    ? s.substring(start, s.length() - 1)
                    : sb.append(s, start, s.length() - 1).toString();
            }

            if (sb == null) {
                sb = new StringBuilder(s.length() - 3);
            }

            sb.append(s, start, end);
            start = end + 1;
        }
    }
}

PARSER_END(Parser)


SKIP : {
    " "
}

TOKEN : {
    < #ALPHA        : ["a"-"z", "A"-"Z"] >
  | < #ESCAPED_CHAR : "\\" ~[] >
}

TOKEN : {
    < UNRESERVED_STR    : ( ~["\"", "'", "(", ")", ";", ",", "=", "<", ">", "!", "~", " "] )+ >
  | < SINGLE_QUOTED_STR : ( "'" ( <ESCAPED_CHAR> | ~["'", "\\"] )* "'" ) >
  | < DOUBLE_QUOTED_STR : ( "\"" ( <ESCAPED_CHAR> | ~["\"", "\\"] )* "\"" ) >
}

TOKEN : {
    < AND        : ( ";" | " and ") >
  | < OR         : ( "," | " or " ) >
  | < LPAREN     : "(" >
  | < RPAREN     : ")" >
  | < COMP_FIQL  : ( ( "=" (<ALPHA>)* ) | "!" ) "=" >
  | < COMP_ALT   : ( ">" | "<" ) ( "=" )? >
}


Node Input():
{
    final Node node;
}
{
    node = Or() <EOF>
    {
        return node;
    }
}

Node Or():
{
    final List<Node> nodes = new ArrayList<Node>(3);
    Node node;
}
{
    node = And() { nodes.add(node); }
    (
        <OR> node = And() { nodes.add(node); }
    )*
    {
        return nodes.size() != 1 ? NodesFactoryAccess.create(factory, LogicalOperator.OR, nodes) : nodes.get(0);
    }
}

Node And():
{
    final List<Node> nodes = new ArrayList<Node>(3);
    Node node;
}
{
    node = Constraint() { nodes.add(node); }
    (
        <AND> node = Constraint() { nodes.add(node); }
    )*
    {
        return nodes.size() != 1 ? NodesFactoryAccess.create(factory, LogicalOperator.AND, nodes) : nodes.get(0);
    }
}

Node Constraint():
{
    final Node node;
}
{
    ( node = Group() | node = Comparison() )
    {
        return node;
    }
}

Node Group():
{
    final Node node;
}
{
    <LPAREN> node = Or() <RPAREN>
    {
        return node;
    }
}

ComparisonNode Comparison():
{
    final String sel;
    final String op;
    final List<String> args;
}
{
    ( sel = Selector() op = Operator() args = Arguments() )
    {
        return NodesFactoryAccess.create(factory, op, sel, args);
    }
}

String Selector(): {}
{
    token = <UNRESERVED_STR> { return token.image; }
    |
    ( token = <DOUBLE_QUOTED_STR> | token = <SINGLE_QUOTED_STR> )
    {
        return unescape(token.image);
    }
}

String Operator(): {}
{
    ( token = <COMP_FIQL> | token = <COMP_ALT> )
    {
        return token.image;
    }
}

List<String> Arguments():
{
    final Object value;
}
{
    ( <LPAREN> value = OptionalCommaSepArguments() <RPAREN> ) { return (List) value; }
    |
    (value = Argument() { return Collections.singletonList((String) value); })?
    {
      return Collections.emptyList();
    }
}

List<String> OptionalCommaSepArguments():
{
    List<String> list = null;
}
{
    [ list = CommaSepArguments() ]
    {
      return list == null ? Collections.emptyList() : list;
    }
}

List<String> CommaSepArguments():
{
    final List<String> list = new ArrayList<String>(3);
    String arg;
}
{
    arg = Argument() { list.add(arg); }
    (
        <OR>
        arg = Argument() { list.add(arg); }
    )*
    {
        return list;
    }
}

String Argument(): {}
{
    token = <UNRESERVED_STR> { return token.image; }
    |
    ( token = <DOUBLE_QUOTED_STR> | token = <SINGLE_QUOTED_STR> )
    {
        return unescape(token.image);
    }
}
